apply plugin: "com.android.application"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "io.vinicius.androidcommon"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Signing
    signingConfigs {
        release {
            storeFile file("../keystore/android-common.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    // Flavors
    flavorDimensions "app"
    productFlavors {
        develop {
            buildConfigField "String", "BASE_URL", "\"https://dev.example.com/\""
        }
        staging {
            buildConfigField "String", "BASE_URL", "\"https://stg.example.com/\""
        }
        production {
            buildConfigField "String", "BASE_URL", "\"https://example.com/\""
        }
    }

    // Others
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    lintOptions {
        lintConfig file("lint.xml")
    }
    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
    }
}

dependencies {
    api fileTree(include: ["*.jar"], dir: "libs")
    api "com.google.android.material:material:1.0.0"
    api "androidx.constraintlayout:constraintlayout:2.0.0-beta1"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "com.jakewharton.timber:timber:4.7.1"
    api "com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar"
    api "com.facebook.android:facebook-login:4.34.0"

    // Android Lifecycle
    api "androidx.lifecycle:lifecycle-extensions:2.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"

    // Android Navigation
    api "android.arch.navigation:navigation-fragment:1.0.0"
    api "android.arch.navigation:navigation-ui:1.0.0"

    // Firebase
    api "com.google.firebase:firebase-core:16.0.9"
    api "com.google.firebase:firebase-auth:17.0.0"

    // Dagger
    api "com.google.dagger:dagger-android-support:2.16"
    kapt "com.google.dagger:dagger-android-processor:2.16"
    kapt "com.google.dagger:dagger-compiler:2.16"

    // RxKotlin
    api "io.reactivex.rxjava2:rxkotlin:2.2.0"
    api "io.reactivex.rxjava2:rxandroid:2.0.2"
    api "com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1"

    // Retrofit & OkHttp
    api "com.squareup.retrofit2:adapter-rxjava2:2.5.0"
    api "com.squareup.retrofit2:converter-jackson:2.5.0"
    api "com.squareup.okhttp3:logging-interceptor:3.12.1"

    // Location (transitive to remove GMS conflicts)
    api("io.nlopez.smartlocation:library:3.3.2") { transitive = false }
    api("io.nlopez.smartlocation:rx:3.3.2") { transitive = false }
}
repositories {
    mavenCentral()
}

apply plugin: "com.google.gms.google-services"